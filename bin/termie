#!/usr/bin/env raku

# termie
#
# requirements:
#   fzf, tmux, raku, Log::Async, Readline
use lib $?FILE.IO.resolve.parent.sibling('lib');

our $VERSION = v0.2.0;

use Log::Async;
use Readline;

use Termie::Actions;
use Termie::Commands;
use Termie::Tester;
use Termie::Tmux;

sub init(Bool :$do-log = True) {
  set-up-tmux;
  init-dynvars;
  load-aliases;
  init-log if $do-log;
}

my ($*timeout, $*delay, $*color, $*trace, $*newlines, $*timing);
my ($*log, $*log-file, $*rl-history-file, @*history, @*shown);
my (%*aliases, $*alias-file);
my ($*pane, $*window);
my $*buffer = 'none';
my $*DEBUG = False;
my %*vars;
my $*greplines;
my $*readline;

sub init-dynvars {
  $*timeout = 5;
  $*delay = 0.1;
  $*color = 'allow';
  $*trace = 'off';
  $*greplines = 3000;
  $*newlines = True;
  my $base = $*HOME.child('.termie');
  .d or .mkdir with $base.IO;
  $*log-file = $base.child('log');
  $*rl-history-file = $base.child('log-rl');
  $*alias-file = $base.child('aliases');
}

sub init-log {
  @*history = $*log-file.IO.lines if $*log-file.IO.e;
  $*log = open :a, $*log-file;
  $*log.out-buffer = 0;
}

sub load-aliases {
  use MONKEY-SEE-NO-EVAL;
  %*aliases = $*alias-file.slurp.lines.map: -> $l {
      my @z = $l.split(':',2);
      @z[0] => EVAL @z[1]
  } if $*alias-file.IO.e;
}

my %*SUB-MAIN-OPTS = :named-anywhere;

#| interactive mode
multi MAIN() {
  logger.send-to($*ERR, :level( * >= INFO));
  init;
  say "Welcome to termie v$VERSION";
  my $r = Readline.new;
  my $cmd = Termie::Commands.new;
  $*readline := $r;
  $r.using-history;
  $r.read-history(~$*rl-history-file);
  loop {
    my $prompt = do
      if $*timing {
        DateTime.now.hh-mm-ss ~ ' > ';
      } else {
        '> ';
      }
    my $str = $r.readline($prompt);
    last without $str;
    $r.add-history($str);
    $r.write-history(~$*rl-history-file);
    last unless defined($str);
    last if $str eq any( '\\quit', '\\exit' );
    replace-aliases($str);
    replace-vars($str);
    trace "executing: $str";
    $cmd.execute($str);
  }
}

#| run a script
multi MAIN(
  $script,                    #= Name of a script to run
  Bool :t($trace)=False,      #= Run in trace mode
  Bool :d($debug)=False,      #= Run in debug mode
  Bool :$report=False,        #= Print a test report at the end
) {
  if $script && not %*ENV<TMUX> {
    exit note "to run a script, start tmux first";
  }
  if $trace {
    logger.send-to($*ERR, :level( * >= TRACE));
  } elsif $debug {
    logger.send-to($*ERR, :level( * >= DEBUG));
  } else {
    logger.send-to($*ERR, :level(* >= ERROR));
  }
  init(:!do-log);
  my $tester = Termie::Tester.new;
  my $cmd = Termie::Commands.new;
  $cmd.run-script($script, :$tester);
  $tester.complete;
  $tester.report if $report
}

#| show the version
multi MAIN(
  Bool :v($version) is required    #= Display the version and exit
) {
  say $VERSION;
}

#| Show usage
multi MAIN('help') {
  say $*USAGE;
}

#| List all the interactive commands
multi MAIN('commands') {
  my $cmd = Termie::Commands.new;
  for $cmd.generate-help().sort(*.<cmd>) {
    next if .<text> ~~ / ^^ \s* \\ script /;
    say .<text>;
  }
}

